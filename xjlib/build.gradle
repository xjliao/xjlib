/*
 * Copyright (c) 2017 xjliao.me created by xjliao
 * ProjectName: xjl
 * ModuleName: xjlib
 * FileName: build.gradle
 * ClassName: build
 * LastModified: 11/30/17 4:39 PM
 */

import org.aspectj.bridge.IMessage
import org.aspectj.bridge.MessageHandler
import org.aspectj.tools.ajc.Main

apply plugin: 'com.android.library'
apply plugin: 'com.jakewharton.butterknife'

version = "1.0.0"

android {
    flavorDimensions "default"
    packageBuildConfig(false)
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    lintOptions {
        abortOnError false
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.1'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "String", "HOST", "\"127.0.0.1\""
            buildConfigField "int", "PORT", "80"
        }

        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "String", "HOST", "\"127.0.0.1\""
            buildConfigField "int", "PORT", "80"
        }
    }
}

android.libraryVariants.all { variant ->
    JavaCompile javaCompile = variant.javaCompile
    javaCompile.doLast {
        String[] args = ["-showWeaveInfo",
                         "-1.8",
                         "-inpath", javaCompile.destinationDir.toString(),
                         "-aspectpath", javaCompile.classpath.asPath,
                         "-d", javaCompile.destinationDir.toString(),
                         "-classpath", javaCompile.classpath.asPath,
                         "-bootclasspath", project.android.bootClasspath.join(
                File.pathSeparator)]

        MessageHandler handler = new MessageHandler(true);
        new Main().run(args, handler)

        def log = project.logger
        for (IMessage message : handler.getMessages(null, true)) {
            switch (message.getKind()) {
                case IMessage.ABORT:
                case IMessage.ERROR:
                case IMessage.FAIL:
                    log.error message.message, message.thrown
                    break;
                case IMessage.WARNING:
                case IMessage.INFO:
                    log.info message.message, message.thrown
                    break;
                case IMessage.DEBUG:
                    log.debug message.message, message.thrown
                    break;
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'com.android.support:appcompat-v7:27.0.1'
    api 'com.android.support:design:27.0.1'
    api 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.3.0') {
        exclude module: 'stax-api'
    }
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.google.dagger:dagger:2.5'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    api 'com.jakewharton:butterknife:9.0.0-SNAPSHOT'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-SNAPSHOT'
    implementation 'com.squareup.okio:okio:1.10.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.6'
    implementation 'com.squareup:otto:1.3.8'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.8.9'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.8.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.8.9'
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.kyleduo.switchbutton:library:1.4.4'
    implementation "com.daimajia.swipelayout:library:1.2.0@aar"
    implementation 'com.daimajia.androidanimations:library:1.1.2@aar'
    implementation 'com.nineoldandroids:library:2.4.0'
    implementation 'org.aspectj:aspectjrt:1.8.9'
}

apply from: 'install.gradle'
apply from: 'bintray.gradle'