/*
 * Copyright (c) 2017 xjliao.me created by xjliao
 * ProjectName: xjl
 * ModuleName: xjlib
 * FileName: bintray.gradle
 * ClassName: bintray
 * LastModified: 10/19/17 8:50 AM
 */

apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/xjliao/xjlib'
def gitUrl = 'https://github.com/xjliao/xjlib.git'
group = "me.xjliao"
version = '1.0.0'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    override = true

    pkg {
        repo = 'maven'
        name = 'xjlib'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/8/docs/api"
    }
}