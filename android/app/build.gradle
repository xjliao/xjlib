/*
 * Copyright (c) 2017 xjliao.me created by xjliao
 * ProjectName: xjl
 * ModuleName: app
 * FileName: build.gradle
 * ClassName: build
 * LastModified: 11/30/17 4:39 PM
 */

project.ext.react = [
        bundleAssetName: "index.bundle",
        entryFile: "src/index.js",
        root: "../../",
        inputExcludes: ["android/**", "ios/**"]
]

apply plugin: 'com.android.application'

apply from: 'tinker-support.gradle'

apply from: "../../node_modules/react-native/react.gradle"

apply from: "../../node_modules/react-native-code-push/android/codepush.gradle"

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    signingConfigs {
        debug {
            storeFile file(STORE_FILE)
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storePassword STORE_PASSWORD
        }

        release {
            storeFile file(STORE_FILE)
            keyAlias KEY_ALIAS
            keyPassword KEY_PASSWORD
            storePassword STORE_PASSWORD
        }
    }

    productFlavors {
        xjl {
            applicationId "me.xjliao.xjl"
            resValue "string", "app_name", "xjl"
            manifestPlaceholders = [APP_ID           : applicationId
            ]

            buildConfigField "String", "APP_ID", "\"" + applicationId + "\""
            versionName vn
            versionCode vc
        }
    }

    lintOptions {
        abortOnError false
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "me.xjliao.xjl"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            buildConfigField "String", "CODEPUSH_KEY", '""'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }

        release {
            buildConfigField "String", "CODEPUSH_KEY", '"' + CODEPUSH_KEY_PRODUCTION + '"'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        releaseStaging {
            buildConfigField "String", "CODEPUSH_KEY", '"' + CODEPUSH_KEY_STAGING + '"'
            minifyEnabled false
            signingConfig signingConfigs.release
            matchingFallbacks = ['release']
        }
    }


    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.productFlavors[0].name}-${variant.buildType.name}" +
                    "-v${variant.productFlavors[0].versionName}-" + "${buildTimeStamp}.apk"
        }
    }
}

dependencies {
    implementation project(':xjlib')
    implementation project(':react-native-code-push')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation  'com.facebook.react:react-native:0.57.1'
    implementation 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    implementation 'com.jakewharton:butterknife:8.5.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.0'
}
